

rule row_Micro_Exon_reads:
    input:
        "" + config["Genome_fasta"],
        "Round1/{sample}.sam.pre_processed"
    output:
        temp("Round1/{sample}.sam.row_ME"),
        temp("Round1/{sample}.sam.row_ME.fastq")
    conda:
        "../envs/core.yaml"
    shell:
        "python2 src/row_ME.py {input} > {output[0]}"


rule hisat2_Genome_index:
    input:
        config["Genome_fasta"]
    output:
        temp("data/Genome.1.ht2")
    threads: 5
    conda:
        "../envs/core.yaml"
    shell:
        "hisat2-build {input} data/Genome"


rule hisat2_to_Genome:
    input:
        "Round1/{sample}.sam.row_ME.fastq",
        "data/Genome.1.ht2"
    output:
        temp("Round1/{sample}.sam.row_ME.Genome.Aligned.out.sam")
    threads: 1
    conda:
        "../envs/core.yaml"
    shell:
        "hisat2 -x data/Genome -U {input[0]} > {output}"


rule Round1_filter:
    input:
        config["Genome_fasta"],
        "Round1/{sample}.sam.row_ME",
        "Round1/{sample}.sam.row_ME.Genome.Aligned.out.sam",
        "data/GT_AG_U2_5.pwm",
        "data/GT_AG_U2_3.pwm",
        config["conservation_bigwig"]
    params:
        ME_len = config["ME_len"]
    output:
        protected("Round1/{sample}.sam.row_ME.filter1")
    conda:
        "../envs/core.yaml"
    shell:
        "python2 src/ME_filter1.py {input} {params.ME_len} > {output}"


rule Micro_Exon_table:
    input:
        expand("Round1/{sample}.sam.row_ME.filter1", sample=DATA )
    output:
        protected("Round1/TOTAL/TOTAL.sam.row_ME.filter1.ME_centric")
    conda:
        "../envs/core.yaml"
    shell:
        "cat {input} > Round1/TOTAL/TOTAL.sam.row_ME.filter1 &&"
        "python2 src/ME_centric_table.py Round1/TOTAL/TOTAL.sam.row_ME.filter1 > {output}"


rule Micro_Exon_Tags:
    input:
        "Round1/ME_TAGs.fa",
        "Round1/TOTAL/TOTAL.sam.row_ME.filter1.ME_centric"
    output:
        "Round2/ME_canonical_SJ_tags.de_novo.fa"
    conda:
        "../envs/core.yaml"
    shell:
        "python2 src/Micro_exons_tags.py  {input} > {output}"
