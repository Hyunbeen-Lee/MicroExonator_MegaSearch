
comparison_names = whippet_delta.keys()


if "whippet_delta" in config:

      if str2bool(config.get("Only_whippet", False)):
            rule differential_inclusion:
                input:
                    expand("Whippet/Delta/{comparison_name}.diff.gz", comparison_name=comparison_names)
      else:
            rule differential_inclusion:
                input:
                    expand("Whippet/Delta/{comparison_name}.diff.microexons", comparison_name=comparison_names),
                    expand("Whippet/Delta/{comparison_name}.diff.ME.microexons", comparison_name=comparison_names)


rule whippet_delta:
    input:
        lambda wildcards : expand("Whippet/Quant/{sample}.psi.gz", sample= whippet_delta[wildcards.comparison_name]["A"].split(",")),
        lambda wildcards : expand("Whippet/Quant/{sample}.psi.gz", sample= whippet_delta[wildcards.comparison_name]["B"].split(","))
    output:
        "Whippet/Delta/{comparison_name}.diff.gz"
    params:
        bin = config["whippet_bin_folder"],
        a = lambda wildcards : ",".join(expand("Whippet/Quant/{sample}.psi.gz", sample= whippet_delta[wildcards.comparison_name]["A"].split(","))),
        b = lambda wildcards : ",".join(expand("Whippet/Quant/{sample}.psi.gz", sample= whippet_delta[wildcards.comparison_name]["B"].split(","))),
        o = lambda wildcards : "Whippet/Delta/" + wildcards.comparison_name
    shell:
        "julia {params.bin}/whippet-delta.jl -a {params.a} -b {params.b} -o {params.o}"



rule whippet_delta_ME:
    input:
        lambda wildcards : expand("Whippet/Quant/{sample}.psi.ME.gz", sample= whippet_delta[wildcards.comparison_name]["A"].split(",")),
        lambda wildcards : expand("Whippet/Quant/{sample}.psi.ME.gz", sample= whippet_delta[wildcards.comparison_name]["B"].split(","))
    output:
        "Whippet/Delta/{comparison_name}.ME.diff.gz"
    params:
        bin = config["whippet_bin_folder"],
        a = lambda wildcards : ",".join(expand("Whippet/Quant/{sample}.psi.ME.gz", sample= whippet_delta[wildcards.comparison_name]["A"].split(","))),
        b = lambda wildcards : ",".join(expand("Whippet/Quant/{sample}.psi.ME.gz", sample= whippet_delta[wildcards.comparison_name]["B"].split(","))),
        o = lambda wildcards : "Whippet/Delta/" + wildcards.comparison_name + ".ME"
    shell:
        "julia {params.bin}/whippet-delta.jl -a {params.a} -b {params.b} -o {params.o} "


rule delta_ME_from_whippet:
    input:
        "Whippet/Index/whippet.jls.exons.tab.gz",
        "Whippet/Delta/{comparison_name}.diff.gz",
        "Report/out.high_quality.txt"
    output:
        "Whippet/Delta/{comparison_name}.diff.microexons"
    shell:
        "python src/whippet_delta_to_ME.py {input} > {output}"

rule delta_ME_from_MicroExonator:
    input:
        "Whippet/Index/whippet.jls.exons.tab.gz",
        "Whippet/Delta/{comparison_name}.ME.diff.gz",
        "Report/out.high_quality.txt"
    output:
        "Whippet/Delta/{comparison_name}.diff.ME.microexons"
    shell:
        "python src/whippet_delta_to_ME.py {input} > {output}"
